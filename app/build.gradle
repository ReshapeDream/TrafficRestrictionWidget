apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.neil.trafficrestrictionwidget"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode getMyVersionCode()
        versionName getMyVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    //加载local.properties
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    signingConfigs {
        neilConfig {
            keyAlias properties.getProperty("keyAlias")
            keyPassword properties.getProperty("keyPassword")
            storeFile file(properties.getProperty("storeFile"))
            storePassword properties.getProperty("storePassword")
        }
    }

    buildTypes {
        release {
            //是否混淆
            minifyEnabled false
            //混淆文件的配置
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //引用的签名文件
            signingConfig signingConfigs.neilConfig
            //不用生成调试包
            debuggable false
            //优化apk
            zipAlignEnabled true
        }
    }

    //修改APK的名字
    applicationVariants.all {
        variant ->
            variant.outputs.all { output ->
                if (variant.buildType.name.equals('release')) {
                    outputFileName = '京限行-' + getMyVersionName() + '_' + getMyVersionCode() + "-" + buildTime() + '-release.apk'
                }
            }
    }
}

String getMyVersionName() {
    return "1.0"
}

int getMyVersionCode() {
    return 1
}

//获取时间戳
def buildTime() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmm')
    return formattedDate
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.squareup.okhttp3:okhttp:4.1.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.x.x'
    implementation 'com.google.code.gson:gson:2.8.5'
}
